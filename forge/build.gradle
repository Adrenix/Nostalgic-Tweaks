plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }

    // Embeddium Support
    modCompileOnly "maven.modrinth:embeddium:${rootProject.neoforge_embeddium_version}"

    // Apple Skin Support
    modCompileOnly "maven.modrinth:appleskin:${rootProject.neoforge_appleskin_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version,
                "mod_author": rootProject.mod_author,
                "mod_id": rootProject.mod_id,
                "mod_name": rootProject.mod_name,
                "mod_source": rootProject.mod_source,
                "mod_issues": rootProject.mod_issues,
                "mod_license": rootProject.mod_license,
                "mod_credits": rootProject.mod_credits,
                "mod_curse": rootProject.mod_curse,
                "mod_modrinth": rootProject.mod_modrinth,
                "mod_donate": rootProject.mod_donate,
                "mod_discord": rootProject.mod_discord,
                "mod_description": rootProject.mod_description,
                "architectury_version": rootProject.architectury_version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    atAccessWideners.add("nostalgic_tweaks.accesswidener")
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

jar {
    archivesBaseName = "${rootProject.archives_base_name}-mc${rootProject.minecraft_max_version}-NeoForge"
    archiveClassifier = "dev"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
