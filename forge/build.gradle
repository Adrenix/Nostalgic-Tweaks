plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
        
        mixinConfigs = [
            "mixin.nostalgic_tweaks-common.json",
            "mixin.nostalgic_tweaks-common-client.json",
            "mixin.nostalgic_tweaks-common-widen.json",
            "mixin.nostalgic_tweaks-forge.json"
        ]
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Apple Skin Support
    modCompileOnly "maven.modrinth:appleskin:${rootProject.forge_appleskin_version}"

    // Fuzzy Searching
    common "me.xdrop:fuzzywuzzy:${rootProject.fuzzy_search_version}"
    shadowCommon "me.xdrop:fuzzywuzzy:${rootProject.fuzzy_search_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version,
            "mod_author": rootProject.mod_author,
            "mod_id": rootProject.mod_id,
            "mod_name": rootProject.mod_name,
            "mod_source": rootProject.mod_source,
            "mod_issues": rootProject.mod_issues,
            "mod_license": rootProject.mod_license,
            "mod_credits": rootProject.mod_credits,
            "mod_curse": rootProject.mod_curse,
            "mod_modrinth": rootProject.mod_modrinth,
            "mod_donate": rootProject.mod_donate,
            "mod_discord": rootProject.mod_discord,
            "mod_description": rootProject.mod_description,
            "architectury_version": rootProject.architectury_version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"

    // Relocate fuzzy package to be under N.T package.
    // This minimizes the chance of conflicting with other mods that may include the same dependencies
    relocate "me.xdrop", "${project.maven_group}.relocated.xdrop"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archivesBaseName = "${rootProject.archives_base_name}-${rootProject.minecraft_range}-Forge"
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
